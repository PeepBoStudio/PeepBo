@camera set:*.false
@back StreetDay
@bgm 메인01
@wait 2
J: 아니~ 무슨 무인도도 아니고 어떻게 개미 한 마리도 안 보이냐고오~!
@printer Narration time:0
20분 째 무거운 캐리어를 끌고 섬을 돌아다녔지만,
주민들의 머리카락 조차 보이지 않았다
아무리 작은 섬이라지만 어쩜 이렇게 휑할 수 있지?
자급자족 하는 섬이라고 들었는데 다들 일하러 갔나?
하긴 농사는 새벽부터 하니까
J: 아이참, 그래도 이건 너무 심한 거 아니야?
@printer Narration time:0
지도라도 찾아보기 위해 휴대폰을 켰다
@char Phone.impossible position:0,-2.5 scale:0.5
@sfx 101_폰_에러음
J: 으악, 심지어 핸드폰도 안 터지잖아
@stopSfx 101_폰_에러음
@hide Phone
J: 주변에 뭔가 도움될만 한 게 없을끼?
; ------------ 탐색 -------------
@hidePrinter 
@showUI StartRoomModeUI time:1
@wait i
@hideUI StartRoomModeUI time:1
@back id:RoomBackground1-1
# RoomStart
@room scriptname:"Script101" label:"RoomBack"
# RoomWait
@stopscript
@stop

# 야생화
J: 여긴 눈 돌리는 곳마다 꽃이 있네
@printer Narration time:0
평범한 야생화지만 누군가 조경이라도 한 것처럼 곳곳에 아름답게 피어있다.
@gosub .RoomWait

# 집
J: 생각보다 집들이 많네, 그런데 사람은 한 명도 보이지가 않아…
@gosub .RoomWait

# 동백꽃
J: 어? 이건 동백꽃인가?
@printer Narration time:0
꽃을 향해 손을 뻗으려는 순간,
@showUI Test
@sfx 101_고양이
; 진동
@spawn ShakeRoomBackground_101
; 집중선 효과
@hideUI Test
J: 으악!
J: 깜짝이야, 고양이잖아?
@printer Narration time:0
그래도 해록도에 온지 1시간 만에 살아있는 뭔가를 보기는 했네…
@gosub .RoomWait

# 담벼락
J: 어? 이건 뭐지?
@printer Narration time:0
담벼락에 무언가 붙어있었다.
@hidePrinter
@showUI 1-1RoomPopup
@wait i
J: 새벽기도 시간 안내…
J: 밑에 이 이름들이랑 숫자는 뭐지?
J: 주이… 목록? 주민을 잘 못 쓴걸까?
@hideUI 1-1RoomPopup
J: 으, 무슨 말인지 하나도 모르겠다. 
@find name:"담벼락"
@gosub .RoomWait

# 지붕
@printer Narration time:0
색이 바랜 초록 지붕, 
명패에 쓰여진 이름은 아주 흐릿해져 읽기 어려웠지만 주소는 남아있었다.
J: 해록도리 12번지?
@printer Narration time:0
집 안에 누군가 있을 지도 몰라, 두드려보자.
; 문 두드리는 효과음
@camera offset:-2.5,1.5,-5 zoom:0.5
@sfx 101_노크 
J: 계세요? 말씀 좀 여쭤보려고요!
@stopSfx 101_노크
@printer Narration time:0
…
@sfx 101_노크 
J: 계세요오!
@stopSfx 101_노크
@printer Narration time:0
안쪽에선 아무 소리도 들리지 않는다.
@camera offset:0,0 zoom:0
@find name:"지붕"
@gosub .RoomWait

; ------------ 탐색 -------------


; 탐색 종료
# RoomBack
@hide RoomBackground1-1
@showUI ExitRoomModeUI time:1.0
@hideUI ExitRoomModeUI
@printer Narration time:0
이제 더는 둘러볼 것도 없어보여
다리도 아프고 짐도 무겁고... 어떻게 하지?
@choice "사람이 올 때까지 가만히 기다린다" goto:.Wait
@choice "일단 계속 돌아다녀본다" goto:.Wander
@hidePrinter 
@stop

# Wait
일단 누구든 올 때까지 기다려봐야지
…
……
시계를 보니 벌써 20분이나 흘렀다
안되겠다 계속 이대로 있을 순 없어!
@goto .Scream
# Wander
일단 계속 돌아다녀봐야지
…
……
하염없이 직진만 한지 20분 째…
길가엔 사람 그림자도 보이지 않는다
# Scream
@hidePrinter
@back StreetDay visible:false wait:false
@back Black time:1
@back Black visible:false wait:false 
@back StreetDay time:2
J: 좋아, 이제 남은 방법은 하나뿐이야
J: 하압-!
@printer Narration time:0
나는 숨을 최대한 깊게 들이마셨다
@spawn ShakeCamera
J: 저기요오오! 아무도오오 없어요오오!
J: 도와-주세요오오오!
First: 귀청 떨어지겠네
@spawn ShakeCamera
; 진동
; 당황 효과음
J: 으악!
@printer Narration time:0
갑자기 들려오는 목소리에 깜짝 놀라 스텝이 꼬이고 말았다
저항할 새도 없이 땅과 충돌하려는 순간,
누군가 내 팔목을 붙들었다
@hidePrinter
;@stopBgm 메인01
;@sfx 101_고결_테마
@back StreetDay visible:false
@movie GMovie time:0.5
@back GIllust time:0.3
@wait i
곧게 뻗은 눈썹, 색소가 옅은 눈동자가 담긴 날카로운 눈매
티끌 만한 오차도 없이 완벽하게 자리잡은 남자의 이목구비에선,
왠지 모를 오만함이 느껴졌다
이 작은 섬에 이토록 잘생긴 남자라니…
@back GIllust visible:false time:0 wait:false
@back StreetDay
J: 누, 누구…
@char G.Body>기본,Face>무표정 position:-0.5,-15.5
@printer Narration time:0
충돌 직전의 나를 붙잡은 이 그림 같은 남자는,
어이없다는 표정으로 나를 훑어보며 말했다
First: 고맙다는 말이 먼저일텐데?
J: 앗 네! 감사합니다. 그런데 누구세요?
@char G.Body>팔짱,Face>무표정
G: 내가 할 질문 아닌가?
@char G.Body>기본,Face>찡그림
G: 그쪽은 누군데 그렇게 해록도 떠나가라 소리를 지르고 있어?
@char G.Body>기본,Face>무표정
J: 제가 먼저 물어봤잖아요
@printer Narration time:0
자리에서 일어나 그를 흘겨봤다
와, 근데 진짜 잘 생겼잖아?
@char G.Body>팔짱,Face>비웃음
G: 잘생긴거 아니까 너무 그렇게 뚫어져라 보지말지?
@printer Narration time:0
재수도 없고…

J: 하하, 혹시 여기 주민이세요? 제가 방금 해록도에 들어왔는데 여쭤볼게 있어서요
@char G.Body>기본,Face>무표정
G: 주민은 아니고 주민 조사하러 나온 공무원
@char G.Body>팔짱,Face>무표정
G: 여기 외지인은 안 받는다는데 그쪽이야 말로 누구?
J: 그냥 휴양하러 온 관광객인데요
@printer Narration time:0
남자는 아주 황당한 소리를 들었다는 듯이 되물었다
@char G.Body>팔짱,Face>기본
G: 무슨 이런 오지에 관광을 와?
J: 아, 아무튼 며칠 묵을 곳을 찾고 있어요. 여기 여관 같은 곳이 있을까요?
@char G.Body>기본,Face>기본
G: 이런 시골에 여관이 있을리가. 뭐, 다 쓰러져가는 마을회관이 있긴 하지
@printer Narration time:0
마을회관이라니, 부탁만 잘하면 머물게 해줄 수도 있겠다
근데 거기까진 또 어떻게 가지?
J: 저 혹시… 마을회관이 어느 쪽에 있나요?
@printer Narration time:0
@char G.Body>기본,Face>무표정
남자는 탐탁치 못하다는 표정으로 어딘가를 가리키며 말했다
@char G.Body>팔짱,Face>기본
G: ...직진하다보면 길 끝에 건물 하나 있어
J: 감사합니다.
@printer Narration time:0
잘 모르겠지말 일단 가다보면 나오겠지
인사를 하고 몸을 돌려 남자가 알려준 방향으로 걷기 시작했다
@hide G

;@stopSfx 101_고결_테마
;@bgm 메인01
…
@sfx 101_발자국_소리
근데…
뭐지, 자꾸 뒤에서 들려오는 발 소리는…?
@stopSfx 101_발자국_소리
J: 왜… 왜 자꾸 따라오세요?
@printer Narration time:0
나는 걸음을 멈추고 남자를 향해 물었다
@char G.Body>기본,Face>무표정 position:-0.5,-15.5
G: 그쪽 안 따라가는데?
J: 그럼 어디가시는 데요?
@char G.Body>팔짱,Face>거만
G: 마을회관
J: 으, 따라오시는 거 맞잖아요!
@char G.Body>손머리,Face>비웃음
G: 내가 널 왜 따라가, 나도 거기에 머물고 있거든?
@printer Narration time:0
남자는 어이없다는 듯 웃으며 말했다.
뭐야 진작 말하던가.
다시 뒤돌아 회관으로 가려다 잠시 캐리어를 끌던 손을 주물렀다.
비포장 된 길에서 한참 캐리어를 끌어서 그런지 손목이 시큰했다.
@char G.Body>팔짱,Face>무표정
G: …
@char G.Body>손머리,Face>무표정
G: 가지가지하네
@printer Narration time:0
@hide G
; 고결 실루엣
남자는 한숨을 쉬더니 내 캐리어를 낚아채더니 앞장 서 걸었다
J: 엇 괜찮은데! 감사합니다!
@printer Narration time:0
남자는 대답없이 묵묵히 캐리어를 끌었다
뭐지, 이 친절한 듯 불친절한 행동은?
난 거의 뛰다시피 걸으며 그와 속도를 맞췄다
;@hide 고결 실루엣
@char G.Body>기본,Face>기본 position:-0.5,-15.5
J: 그, 성함이 어떻게 되세요?
@char G.Body>팔짱,Face>거만
G: 고결
J: 엇!
@printer Narration time:0
남자의 이름을 듣는 순간 깜짝 놀랄 수 밖에 없었다
생전 처음 듣는 이름과 처음 보는 얼굴인데 남자와 더없이 어울렸던 탓이다
고결은 내 놀란 기색을 느꼈는지 짧게 웃으며 말했다
G: 얼굴이랑 꽤나 어울리는 이름이긴 하지
@printer Narration time:0
난 순간 할 말을 찾지 못해 입을 꾹 닫고 말을 골랐다
@choice "네... 정말  잘 어울리네요..." goto:.A
@choice "자기애가 되게 강하신가봐요?" goto:.B
@hidePrinter 
@stop

# A
; 호감도 +5
J: 네에… 정말 어울리네요…
@char G.Body>손머리,Face>비웃음 position:-0.5,-15.5
G: 표정은 아닌데, 재수 없다고 생각했지?
J: 아, 아닌데요?
@printer Narration time:0
뭐야 어떻게 알았지?!
@goto .Con1

# B
J: 자기애가 되게… 강하신가봐요…?
@char G.Body>팔짱,Face>비웃음 position:-0.5,-15.5
G: 꽤 들어 그런 말
@printer Narration time:0
뭐지 이 은근하게 느껴지는 재수없음은?
G: 재수 없다고 생각했지
J: 오, 들켰다.

# Con1
@printer Narration time:0
난 장난스레 대답했지만,
사실 그의 말마따나 얼굴과 꽤나 잘 어울리는 이름이었다
@char G.Body>기본,Face>무표정 position:-0.5,-15.5
G: 근데 정말 여긴 왜 왔어?
G: 보통 혼자 이런 아무 것도 없는 섬엔 안 오지않나?
J: 관광왔다니까요? 저 대학 막 졸업했거든요
J: 졸업여행 삼아 온 거예요
@printer Narration time:0
@char G.Body>팔짱,Face>무표정
고결은 별로 내 말을 믿는 눈치가 아니었다
시시콜콜한 대화를 하며 10분 정도 걸었을까-
@hideChars
@hidePrinter 
@back StreetDay visible:false time:0 wait:false
@back HallDay
@wait 1
; 다이어리에 장소발견
; 발견 효과음
@showUI HallFindUI
@wait i
@hideUI HallFindUI
J: 감사합니다
@char G.Body>팔짱,Face>거만 position:-0.5,-15.5
G: 곧 주민들이 올테니까 아무나 붙잡고 도와달라고 해
@char G.Body>팔짱,Face>기본
G: 얼마나 머물 생각이야?
J: 한 달 정도 있을 것 같아요
@printer Narration time:0
@char G.Body>기본,Face>거만
그는 거만하게 웃으며 말했다
G: 아는 지 모르겠지만 해록도 사람들은 외지인에게 친절한 편이 아니야. 
@char G.Body>팔짱,Face>비웃음
G: 아마 너한테 제일 친절한 사람은 나일걸?
J: 아하...
@printer Narration time:0
그런데 어쩌라는 거지?
@choice "...네, 감사합니다..." goto:.C
@choice "근데 왜 자꾸 반말을 하세요?" goto:.D
@hidePrinter 
@stop

# C
J: ... 네에... 감사합니다
@printer Narration time:0
엎드려 절받는 걸 좋아하는 편인가 보네
@goto .Con2

# D
J: 근데 왜 자꾸 반말을 하세요?
@printer Narration time:0
@char G.Body>기본,Face>거만
남자는 또 내 말에 대답하지 않은 채 으쓱 어깨만 끄덕일 뿐이었다
J: 윽, 진짜…! 참자 참아!
@printer Narration time:0
결의 말에 대꾸하는 대신 눈으로 그를 흘겨봤다

# Con2
@hide G
@printer Narration time:0
그나저나 여기가 마을의 중심인 것 같은데 여기라면 핸드폰이 터지지 않을까?!
@char Phone.impossible position:0,-2.5 scale:0.5
; 진동
서둘러 핸드폰을 켜봤지만 기대완 달리 여전히 핸드폰은 먹통이었다.
@hide Phone
@sfx 101_문여는_소리
그때, 누군가 회관문을 열고 들어왔다
@stopSfx 101_문여는_소리
@spawn ShakeBackground
Woman: 아이고!
@printer Narration time:0
날 보자마자 소스라치게 놀라며 뒷걸음질 친 아주머니는,
황급히 문을 닫고 회관을 나갔다.
@char G.Body>팔짱,Face>무표정 position:-0.5,-15.5
G: 봐봐, 여기 사람들은 외지인만 보면 꼭 귀신이라도 본 것처럼 저런다니까
@hide G
@sfx 101_문여는_소리
@printer Narration time:0
곧 다시 문이 열리고, 
방금 나간 아주머니와 또 다른 아저씨 한 명이 함께 들어왔다
@stopSfx 101_문여는_소리
@char Man visible:false avatar:Headman
아저씨는 나를 보며 어색한 미소로 말을 건냈다
Man: 혹시 해록도에 관광오기로 한 아가씨인가?
J: 아, 네 안녕하세요. 저 출입허가서도 받았어요! 이상한 애 아니에요!
@printer Narration time:0
주민들 눈에 경계심이 어려있어 다급히 입꼬리를 최대한 끌어올려 빠르게 말했다
@hide Man wait:false
@char Woman visible:false avatar:People
Woman: 아~ 그 아가씨구나, 관광 온다는 얘기 들었어요.
Woman: 나는 또 처음보는 사람이라 놀랐지 뭐야
Woman: 해록도가 원채 오가는 사람도 없고 주민들도 적어~
Woman: 그래서 누가 들어온다고 하면 우리끼리 다 알거든
@printer Narration time:0
경계가 풀렸는지 아주머니는 인자하게 웃으며 다가왔다
@hide Woman wait:false
@char Man visible:false avatar:Headman
Man: 여기가 다 흙길이라서 가방 끌고 오기 힘들었을 텐데
Man: 선착장에서 조금만 기다리고 있지 그랬어
Man: 아가씨 오늘 온다는 얘기 듣고 지금 다른 주민이 선착장에 마중나갔는데
J: 정말요? 제가 배에서 내리자마자 돌아다녀서 길이 엇갈렸나봐요.
J: 마중까지 나와주시고 정말 감사합니다.
@printer Narration time:0
뒤에서 은근한 시선이 느껴져 돌아보니,
결이 묘한 눈으로 주민들을 빤히 쳐다보고 있었다.
왜 저렇게 쳐다보지?
@hide Man wait:false
@char Woman visible:false avatar:People
Woman: 여기 얼마나 있을 거에요? 해록도가 그 뭐냐,
Woman: 무슨 보호? 생태보호? 그걸로 지정 돼 있는 건 알죠?
Woman: 나무들이 건강해서 공기도 좋고 해안가 경치도 정말 좋아요.
Woman: 오래오래 있으면 좋겠다, 오랜만에 외지인 보니까 정말 반갑네~
@hide Woman wait:false
@char Man visible:false avatar:Headman
Man: 그러게 말이야, 오랜만에 온 귀한 손님이네
Man: 머물 곳은 우리가 다 마련해 놨으니까 따라와요
@hide Man
@printer Narration time:0
아저씨를 따라 회관을 나서려다 뒤를 돌아 고결에게 말했다
@char G.Body>기본,Face>무표정 position:-0.5,-15.5
J: 데려다 주셔서 감사합니다 또 뵈어요!
@printer Narration time:0
@char G.Body>기본,Face>비웃음
결은 씨익 웃으며 대답을 대신했다.
@hide G
어? 그런데 이상하다,
해록도 주민들은 외지인을 경계한다고 하지 않았나?

@back HallDay visible:false time:0 wait:false
@back StreetDay position:-8.3,0
@wait 2
아저씨는 나를 데려다주는 길에 해록도에 관한 몇 가지 이야기와
본인이 해록도의 이장이라는 것을 알려줬다.
회관을 나와 15분 쯤 걸었을까,
이장님과 함께 도착한 곳은 소담스런 2층짜리 주택이었다.
지나오면서 본 해록도의 집 중에 제일 큰 것 같은데, 
어딘지 모르게 쓸쓸함이 느껴지는 집이었다.
@hidePrinter
; 발견 효과음
@showUI NHouseUI
@wait i
@hideUI NHouseUI

@sfx 101_노크
Headman: 나연 자매 있어? 손님 왔어!
@stopSfx 101_노크
@sfx 101_문여는_소리
@printer Narration time:0
잠시 뒤 문이 열리며 앳되보이는 여자아이가 고개를 빼꼼 내밀었다
@stopSfx 101_문여는_소리
@char N.Face>기본,Body>기본 position:0,-13.5
여자애는 인사도 없이 무표정한 얼굴로 가만히 나를 관찰하듯 쳐다볼 뿐이었다
아니 여긴 왜 나를 자꾸 빤히 쳐다보는 사람 밖에 없지?
외지인이 드물다더니, 내가 신기한가?
J: 안녕하세요, 김지안이라고 해요!
Headman: 해록도에서 지내는 동안 이 집에 머물면 돼요. 
Headman: 나연 자매 혼자 사는 집이라서 불편하진 않을 거야
@hide N
@printer Narration time:0
나연은 따라오라며 고개짓을 한 뒤 성큼 성큼 집 안으로 들어갔다
나는 나연의 뒤를 쫓으며 이장님을 향해 고개를 꾸벅였다

@back StreetDay visible:false time:0 wait:false
@back JianRoomDay position:0,0
@char N.Face>기본,Body>기본 position:0,-13.5
N: 여기 쓰면 돼.
@hide N
@printer Narration time:0
나연은 방까지 날 안내한 뒤 대답도 듣지 않고 돌아나갔다.
깔끔하게 정돈된 방이었다.
본래는 숙박용이 아니라 개인이 쓰던 방인 듯했다.
벽지는 과거에 있었던 가구 모양에 맞추어 바래있었고,
나무를 깎아 만든 십자가가 벽에 걸려 있었다.
그걸 보자 급격히 피곤이 몰려와 침대에 누웠다.
생각했던 것보다 푹신해서 몸이 나른해졌다.
J: 아, 이대로 잠들어버릴지도 모르겠어…
@back JianRoodDay visible:false
@hidePrinter
@back Black time:1
@wait 1
@loadScene MainScene