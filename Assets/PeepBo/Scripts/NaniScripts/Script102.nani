@camera set:*.false
@back Black
; 메인 bgm
First: ....어나...
@printer Narration time:0
...
@printer Dialogue time:0
First: 일어...
@printer Narration time:0
… 뭐지? 누가 날 부르나?
@printer Dialogue time:0
First: 야...
@printer Narration time:0
으, 시끄러워…
일어나고 싶은데 몸이 무거워 움직일 수가 없어…
@printer Dialogue time:0
First: 야! 일어나라니까!
; 이불 걷히는 효과음
@back Black visible:false time:0
@back JianRoomDay
N: 진짜 귀찮게 한다, 너.
@printer Narration time:0
익숙한 목소리에 고개를 돌리니.
@char N.Face>기본,Body>기본 position:0,-16
@wait i
@printer Dialogue time:0
@char N.Face>짜증,Body>팔짱 scale:1.2 position:0,-20
N: 벌써 해가 중천인데 아직도 자고 있으면 어떡해? 여기 요양왔어?
@printer Narration time:0
@char N.Face>짜증,Body>팔짱 scale:1 position:0,-16
놀라서 시계를 바라보자 오후 12시가 넘어가고 있었다.
@spawn ShakeBackground wait:false
@printer Dialogue time:0
J: 헉! 거의 12시간을 잤네
J: 깨워줘서 고마워!
@char N.Face>짜증,Body>기본
N: 그 쪽 예뻐서 깨운 거 아니거든?
@char N.Face>짜증,Body>팔짱
N: 밖에 손님 왔어. 신경 쓰여 죽겠으니까 빨리 나가.
J: 응? 내 손님?
@printer Narration time:0
누구지? 어제 만난 그 남자인가, 아니면 이장님?
나는 긴가민가하며 자리에서 일어났다.
@printer Dialogue time:0
@char N.Face>기본,Body>팔짱
N: 야.
J: 응?
N: 나 너 좋아서 우리집에 머물게 한 거 아니고 어쩔 수 없이 떠맡은 거야.
@char N.Face>짜증,Body>기본
N: 그러니까 거슬리는 행동하지 말고 최대한 조용히 있다가 떠나,
N: 나한테나 다른 주민들한테나.
@hide N time:0 wait:false
@spawn ShakeBackground
@printer Narration time:0
나연은 차갑게 쏘아 붙인채 부셔져라 문을 닫고 나갔다.
@printer Dialogue time:0
J: 아니, 나도 꼭 여기 머물고 싶어서 온 건 아니거든? 정말 어이없어….
@printer Narration time:0
그런데 아침부터 찾아온 손님이 대체 누굴까? 해록도에는 나를 찾는 사람이 없을텐데…
일단, 씻고 얼른 나가보자.
@hidePrinter
@back Remine.Dissolve params:100
@wait i
; 쓸쓸 bgm
지안아 이제부터 좋은 것만 보고, 좋은 것만 듣고, 좋은 것만 기억하면서 사는 거야.
기억이란 생각보다 단순해서 내 입맛대로 보관할 수 있거든.
그야말로 신이 내린 선물이지.
뭐? 거짓말이라고? 넌 쪼그만 게 벌써부터 어른을 의심하니.
@hidePrinter
@wait i
@back Remine visible:false time:0
@back StreetDay position:-8.8,0
; 메인 bgm
문을 열자마자 쏟아지는 햇살이 눈이 부셔 나도 모르게 눈을 꾹 감았다.
@printer Dialogue time:0
J: 으아, 눈부셔...
; 눈 깜박이듯
@printer Narration time:0
천천히 감았던 눈을 뜨자 눈꺼풀 사이로 보이는 건….
@hidePrinter
@back StreetDay visible:false time:0 wait:true
@spawn SunShafts wait:false
@back YIllust position:0,0
@wait i
@printer Dialogue time:0
Y: 기다렸어요, 누나. 잘 잤어요?
@despawn SunShafts wait:false
@back YIllust visible:false time:0 wait:true
; 효과음
@back StreetDay position:-8.8,0
@char Y.Face>기본,Body>기본 position:-0.25,-17
J: 설마 그쪽이...제 손님이세요...?
@char Y.Face>웃는,Body>브이
Y: 하핫, 반가워요! 육지에서 손님이 왔다 길래 얼른 보러 왔지요!
@printer Narration time:0
아무리 작은 섬이라도 그렇지, 여기 온 지 하루 만에 나에 대한 소문이 퍼진 건가?
@printer Dialogue time:0
Y: 그런데 누나, 이름이 뭐예요?
@choice 얼버무린다. goto:.First set:y_score=0
@choice 알려준다. goto:.Second set:y_score=5
@hidePrinter 
@stop

# First
J: ….#$*^@...인데요. 
Y: 아~ 김지안 이라고요?
@printer Narration time:0
와, 소름.		
일부러 발음을 잔뜩 뭉갰는데 무슨 귀가 이렇게 좋아?	
@printer Dialogue time:0
@char Y.Face>아련,Body>기본
Y: 김지안이라…
@char Y.Face>놀란,Body>입에손 scale:1.2 position:-0.25,-21
Y: 이름이 누나랑 너무 잘 어울리는 거 아니에요?
Y: 그냥 그 이름은 누나만을 위해 존재해야할 것 같아요!
@char Y.Face>놀란,Body>입에손 scale:1 position:-0.25,-17
J: …
@char Y.Face>웃는,Body>기본
Y: 하하하, 지금 정색한 거예요? 누나 반응 진짜 재밌네요.
@goto .Continue
# Second
J: ... 김지안인데요.
@char Y.Face>놀란,Body>입에손
Y: 와... 진짜 말도 안 돼, 이름이 누나랑 너무 잘 어울리는 거 아니에요?
J: 뭐, 말도 안 될 것까지야...
@char Y.Face>웃는,Body>기본
Y: 하하하, 지금 정색한 거예요? 누나 반응 진짜 재밌네요.
# Continue
J: 저기 그런데,
J: 그쪽은 대체 누구신데요?
@char Y.Face>윙크,Body>기본 scale:1.2 position:-0.25,-21
Y: 전 홍! 유! 겸! 이에요. 스물 한 살이니까 말 편하게 해요. 누나!
@char Y.Face>윙크,Body>기본 scale:1 position:-0.25,-17
@printer Narration time:0
자기가 스물 한 살인 건 그렇다 쳐도 내 나이도 모르면서 왜 누나라고 부르는 거야?
나는 눈동자만 도르륵 굴려 홍유겸이라는 남자를 쭉 훑어봤다.
@char Y.Face>기본,Body>기본
그래도 뭐, 조금 정신사납긴 하지만 나쁜 사람은 아닌 것 같아.
이건 절대 겉모습만 보고 판단한 게 아니다.
웃는 게 조금 귀엽다고 방심한 게 아니라는 말이다. 진짜로.
@printer Dialogue time:0
J: 그…래, 그나저나 나는 왜 찾아온 거야?
@char Y.Face>윙크,Body>브이
Y: 해록도에 처음 왔으니, 섬 구경을 해야 하지 않겠어요?
Y: 사실 제가 해록도의 비공식 마스코트를 맡고 있거든요
Y: 해록도하면 유겸이, 유겸이하면 귀여워, 귀여우면 홍유겸,
J: 응! 그래서?
@printer Narration time:0
멍하니 듣고 있다가는 해가 질 것 같다.
@printer Dialogue time:0
@char Y.Face>웃는,Body>브이
Y: 그러니까! 이 유겸이가 지안 누나만의 특별 가이드를 해주려 왔다는 거죠.
J: 가이드?
@char Y.Face>웃는,Body>기본
Y: 도시에 비해선 작지만, 해록도 골목이 비슷비슷해서 은근 헷갈리거든요!
Y: 주민이 아니면 길을 잃기 쉬워요.
@printer Narration time:0
섬을 안내 해주겠다는 그의 말에 나는 잠시 고민에 빠졌다.
안 그래도 오늘부터 해록도를 탐방하려고 하긴 했지...
자기가 먼저 안내해주겠다고 하는데 나쁠 건 없어 보이기도 하고,
어떡할까?
@choice "어색한 건 딱 질색, 혼자 다닌다" goto:.Alone set:y_score+=0
@choice "의외로 좋은 기회일지도. 같이 다닌다." goto:.Together set:y_score+=5
@hidePrinter 
@stop

# Alone
@printer Narration time:0
역시 처음 보는 사람과 단둘이 다니는 건, 생각만 해도 어색해.
@printer Dialogue time:0
J: 아니, 괜찮아. 나는 혼자 다니는 게 편하거든.
@char Y.Face>울먹,Body>입에손
Y: 앗… 제가 불편해요…?
@printer Narration time:0
그가 갑자기 내 소매 끝을 잡았다.
그리고 푹 숙인 고개를 천천히 드는데...
아... 저 커다랗고 맑은 눈동자.
@printer Dialogue time:0
J: ….
J: ....그래 알겠어, 같이 다니자. 길 헤매는 것보단 낫겠지.
@char Y.Face>윙크,Body>브이
Y: 탁월한 선택이에요!
@goto .Walk
# Together
@printer Narration time:0
그래. 도움 받을 수 있는 건 받자. 시끄럽지만 친절한 애 같으니.
@printer Dialogue time:0
J: 고마워, 시간 괜찮아?
@char Y.Face>윙크,Body>브이
Y: 당연하죠! 오늘 하루는 누나를 위해 비워뒀는걸요?
@printer Narration time:0
저 동글동글한 눈빛을 보고도 차갑게 거절할 수 있는 사람이 어딨겠어?
# Walk
@back StreetDay position:8.8,0
@printer Dialogue time:0
@char Y.Face>기본,Body>기본
Y: 누나가 묵고 있는 곳에서 조금만 더 나오면 바로 큰 길가에요.
@printer Narration time:0
유겸은 새하얀 손가락으로 이곳 저곳을 가리켰다.
@printer Dialogue time:0
@char Y.Face>웃는,Body>기본
Y: 이쪽으로 쭉 내려가면 마을 회관이 나오고, 반대편으로 가면 이장님 댁이에요.
Y: 주민들은 마을 회관에 자주 모이거든요.
@char Y.Face>윙크,Body>기본
Y: 필요한 게 생기거나 궁금한 게 있을 땐 마을 회관으로 가면 돼요
@printer Narration time:0
난 그가 가리키는 곳들을 열심히 기억하려 애썼다.
@printer Dialogue time:0
@char Y.Face>웃는,Body>기본
Y: 흠, 이제 어디 먼저 가볼래요?
@choice 선착장 goto:.Dock set:IsVisitDO+=1
@choice 등대 goto:.Lighthouse set:IsVisitLH+=1
@choice "아니 됐어, 그냥 산책이나 하자" goto:.Forest
@hidePrinter 
@stop

# Dock
J: 선착장 먼저 가보자!
@hide Y
@back DockDay
; 파도소리
; 다이어리에 'N'
@hidePrinter
@toast "장소를 발견했습니다! <선착장>" time:2 wait:false
@wait i
@char Y.Face>기본,Body>기본 position:-0.25,-17
Y: 누나도 알다시피 해록도로 오는 배가 별로 없어요
Y: 이주에 한 번 물자선이 오는 정도?	
J: 그럼 해록도 사람들은 육지로 잘 안 나가?
@char Y.Face>웃는,Body>기본
Y: 특별한 일이 없으면 굳이 안 나가죠
Y: 그런데 그래도 괜찮아요
@char Y.Face>웃는,Body>기본
Y: 주민들 중에 육지에서 온 사람들도 있는데,
@char Y.Face>웃는,Body>브이
Y: 모두 해록도가 훨~씬 살기 좋다고 하더라고요!	
Y: 그리고,
@char Y.Face>웃는,Body>기본
Y: 보다시피 해록도가 너무너무 아름답잖아요?	
@printer Narration time:0
@char Y.Face>아련,Body>기본
유겸은 눈을 내리깐 채 조용히 웃으며 말을 이었다
@printer Dialogue time:0
Y: 한 번 오면... 다신 떠나고 싶지 않을 만큼.
@printer Narration time:0
@char Y.Face>기본,Body>기본
뭐지, 방금 분위기가 갑자기 묘하게 어른스러워졌는데?	
@printer Dialogue time:0
J: 맞아 아름답긴 하더라
J: 누구든 해록도를 보면 여기 머물고 싶다고 생각할 거야
@char Y.Face>웃는,Body>기본
@printer Narration time:0
나는 어제 배 위에서 처음 본 해록도의 전경을 다시 되뇌었다.
@if IsVisitLH==0
    @choice 등대 goto:.Lighthouse set:IsVisitLH+=1
    @choice "아니 됐어, 그냥 산책이나 하자" goto:.Forest
@else
    @choice "아니 됐어, 그냥 산책이나 하자" goto:.Forest
@endIf
@hidePrinter 
@stop

# Lighthouse
@hide Y
@back LighthouseDay
; 파도소리
; 다이어리에 'N'
@hidePrinter
@toast "장소를 발견했습니다! <등대>" time:2 wait:false
@wait i
@printer Narration time:0
등대 근처에 창고로 보이는 건물이 하나 있지만, 굳게 닫혀 있는 듯 하다.
@printer Dialogue time:0
@char Y.Face>기본,Body>기본 position:-0.25,-17
Y: 여긴 진짜 별게 없어요!
Y: 등대 안에 들어가봤자 계단 뿐이죠.
Y: 예전에는 여기로 불법 어선들이 자주 나타났대요.
@char Y.Face>웃는,Body>기본
Y: 그래서 주민들이 돌아가면서 등대지기를 하고 있어요
J: 그렇구나, 너도 해본 적 있어?
@char Y.Face>놀란,Body>입에손
Y: 엑…! 저는 이런 어둡고 음침한 곳과 안 어울리죠….! 그리고 제가 잠도 많은 편이라...
J: 무서워서 빠졌구나.
@char Y.Face>놀란,Body>기본
Y: 당연….이 아니고! 아, 누나!
@printer Narration time:0
그가 꽥 소리를 지르는 바람에 귀가 멀 뻔 했다
@printer Dialogue time:0
@char Y.Face>아련,Body>기본
Y: 아무튼 누나도 여기 혼자 오지말아요!
Y: 새빨간 데다가 엄청 낡아서 뭔가…. 으스스 하다구요!
@char Y.Face>웃는,Body>기본
Y: 자 다음은 어디로 가볼까요?
@if IsVisitDO==0
    @choice 선착장 goto:.Dock set:IsVisitDO+=1
    @choice "아니 됐어, 그냥 산책이나 하자" goto:.Forest
@else
    @choice "아니 됐어, 그냥 산책이나 하자" goto:.Forest
@endIf
@hidePrinter 
@stop

# Forest
@hide Y
@back ForestDay
; 다이어리에 'N'
@hidePrinter
@toast "장소를 발견했습니다! <숲속>" time:2 wait:false
@wait i
@printer Dialogue time:0
J: 으흠. 공기 좋다.
J: 섬에 이런 숲 길이 있었구나. 낮에는 산책하기에도 딱 좋겠어!
@char Y.Face>놀란,Body>입에손 position:-0.25,-17
Y: 누나… 보기보다 액티비티를 좋아하나봐요. 이런 산에서 산책이라니… 
@char Y.Face>아련,Body>기본
Y: 전 여기 오르내릴때마다 힘들어 죽겠던데.
J: 유겸이 넌 보기보다 연약하구나. 길도 고르고 별로 경사도 없는데, 엄살은…
@printer Narration time:0
내가 혀를 끌끌 차자 유겸은 못 들은 척 고개를 돌린다.
@printer Dialogue time:0
J: 근데, 이 위로 쭉 올라가면 뭐가 있는데?
@char Y.Face>기본,Body>기본
Y: 선락원이요, 그냥 교회인데 저희 마을에선 선락원이라고 부르고 있어요
J: 무슨 교회가 산 중턱에 있어?
@char Y.Face>윙크,Body>기본
Y: 그곳이 해록도의 중심이거든요
@printer Narration time:0
@char Y.Face>기본,Body>기본
그때, 맞은 편에서 이장님과 주민 한 분이 내려오는 게 보였다.
@printer Dialogue time:0
Headman: 어어, 유겸이~
Woman: 이쪽은 그 해록도 관광왔다는 아가씨야? 세상에 얼굴 하얀 것 봐. 너무 곱다.
J: 아, 안녕하세요.
Woman: 반가워요. 근데 어디가는 길이야?
Y: 제가 또 한 친절 하잖아요? 육지에서 온 귀한 손님에게 섬을 안내해주고 있었지요.
Headman: 아가씨가 잘 만났네! 우리 유겸이가 얼마나 해록도에 빠삭한 지,
Headman: 이보다 더 유능한 가이드는 없을거야.
Woman: 해록도만 잘 알게? 요리면 요리, 바느질이면 바느질. 못하는 게 없다니까.
@printer Narration time:0
아주 해록도 주민들에게 사랑과 관심을 듬뿍 받고 있나보네?
@printer Dialogue time:0
@char Y.Face>웃는,Body>입에손
Y: 에헤이, 지금 외부인 앞이라고 저 띄워주시는 거죠? 사양말고 더 띄워줘도 돼요. 엣헴.
Woman: 아, 요리하니까 생각났는데
Woman: 유겸이가 알려준 레시피대로 빵을 구웠는데도 자꾸 실패해!
Woman: 왜 그럴까, 으응? 우리 집에 와서 한번만 더 알려줘~
Woman: 이러다 남은 재료들마저 다 버리겠어
@char Y.Face>놀란,Body>기본
Y: 에이 정말! 제가 하란대로 안 한거 아니에요? 진짜 쉬운데…
Woman: 말도 마. 계속 타고, 터지고 난리도 아니더라니까.
@printer Narration time:0
주민의 하소연에 유겸은 제 머리를 긁적이며 내 눈치를 봤다.
@printer Dialogue time:0
@char Y.Face>아련,Body>기본
Y: 흐음… 오늘은 지안 누나를 위해 하루를 비우기로 했으니까, 그럼 내일…
J: 난 괜찮아! 웬만한 곳은 다 둘러봤고
J: 이왕 여기까지 온 거 산책 좀 하다가 돌아가려고.
@char Y.Face>기본,Body>기본
Y: 흐음… 알겠어요!
@printer Narration time:0
유겸은 아쉬운 듯했지만, 이내 주민과 자연스럽게 팔짱을 꼈다.
@printer Dialogue time:0
@char Y.Face>윙크,Body>브이
Y: 자자, 우리 누님은 유겸이의 베이킹 교실로 가보자고요!
@hide Y
Headman: 그럼 지안씨도 조금만 둘러보다 해 지기 전에 어서 들어가요.
J: 네네. 다음에 뵈어요.

@loadScene MainScene